# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/mojo.gni")

group("public") {
  # Meta-target, don't link into production code.
  testonly = true
  deps = [
    ":libmojo_sdk",
    ":sdk",
    mojo_root + "mojo/public/cpp/application:standalone",
    mojo_root + "mojo/public/cpp/bindings",
    mojo_root + "mojo/public/cpp/environment:standalone",
    mojo_root + "mojo/public/cpp/utility",
    mojo_root + "mojo/public/interfaces/bindings/tests:test_interfaces",
    mojo_root + "mojo/public/sky",
  ]

  if (is_linux) {
    deps += [
      mojo_root + "mojo/public/python",
    ]
  }

  if (is_android) {
    deps += [
      mojo_root + "mojo/public/java:system",
      mojo_root + "mojo/public/java:bindings",
    ]
  }
}

group("sdk") {
  deps = [
    mojo_root + "mojo/public/c/system",
    mojo_root + "mojo/public/cpp/application:standalone",
    mojo_root + "mojo/public/cpp/bindings",
    mojo_root + "mojo/public/cpp/environment:standalone",
    mojo_root + "mojo/public/cpp/utility",
    mojo_root + "mojo/public/interfaces/application",
    mojo_root + "mojo/public/js/bindings",
  ]
}

static_library("libmojo_sdk") {
  complete_static_lib = true
  deps = [ ":sdk" ]
}
