# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/mojo.gni")

source_set("environment") {
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [
    "async_waiter.h",
    "logging.h",
    "environment.h",
  ]

  public_deps = [ rebase_path("mojo/public/c/environment", ".", mojo_root) ]

  deps = [
    rebase_path("mojo/public/cpp/bindings:callback", ".", mojo_root),
    rebase_path("mojo/public/cpp/system", ".", mojo_root),
  ]
}

source_set("standalone") {
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [
    "lib/async_waiter.cc",
    "lib/default_async_waiter.cc",
    "lib/default_async_waiter.h",
    "lib/default_logger.cc",
    "lib/default_logger.h",
    "lib/environment.cc",
    "lib/logging.cc",
  ]

  public_deps = [ ":environment" ]

  deps = [
    rebase_path("mojo/public/c/environment", ".", mojo_root),
    rebase_path("mojo/public/cpp/utility", ".", mojo_root),
  ]
}
