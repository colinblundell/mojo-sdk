# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/mojo.gni")

# GYP version: mojo/public/mojo_public.gyp:mojo_application_base
source_set("application") {
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [
    "application_connection.h",
    "application_delegate.h",
    "application_impl.h",
    "connect.h",
    "service_provider_impl.h",
    "interface_factory.h",
    "interface_factory_impl.h",
    "lib/application_connection.cc",
    "lib/application_delegate.cc",
    "lib/application_impl.cc",
    "lib/service_provider_impl.cc",
    "lib/service_connector.cc",
    "lib/service_connector.h",
    "lib/service_registry.cc",
    "lib/service_registry.h",
    "lib/weak_service_provider.cc",
    "lib/weak_service_provider.h",
  ]

  deps = [
    rebase_path("mojo/public/cpp/bindings", ".", mojo_root),
    rebase_path("mojo/public/cpp/environment", ".", mojo_root),
    rebase_path("mojo/public/cpp/system", ".", mojo_root),
    rebase_path("mojo/public/interfaces/application", ".", mojo_root),
  ]
}

# GYP version: mojo/public/mojo_public.gyp:mojo_application_standalone
source_set("standalone") {
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [
    "lib/application_runner.cc"
  ]

  public_deps = [ ":application" ]

  deps = [
    rebase_path("mojo/public/cpp/environment:standalone", ".", mojo_root),
    rebase_path("mojo/public/cpp/utility", ".", mojo_root),
  ]
}

source_set("test_support") {
  testonly = true
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [
    "application_test_base.h",
    "lib/application_test_base.cc",
  ]

  deps = [
    ":application",
    rebase_path("mojo/public/cpp/bindings", ".", mojo_root),
    rebase_path("mojo/public/cpp/environment", ".", mojo_root),
    rebase_path("mojo/public/cpp/system", ".", mojo_root),
    rebase_path("testing/gtest", ".", mojo_third_party_root),
  ]
}

source_set("test_support_standalone") {
  testonly = true
  public_configs = [ rebase_path("mojo/build/config", ".", mojo_root) + ":mojo_sdk" ]
  sources = [ "lib/application_test_main.cc" ]

  public_deps = [ ":test_support" ]
  deps = [
    ":application",
    rebase_path("mojo/public/cpp/environment:standalone", ".", mojo_root),
    rebase_path("mojo/public/cpp/system", ".", mojo_root),
    rebase_path("mojo/public/cpp/utility", ".", mojo_root),
  ]
}
