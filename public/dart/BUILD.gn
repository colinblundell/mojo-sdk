# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("dart") {
  testonly = true
  deps = [
    ":core",
    ":bindings",
  ]
}


group("core") {
  deps = [
    ":mojo_dart_core",
    ":copy_core_library",
  ]
}


shared_library("mojo_dart_core") {
  defines = ["DART_SHARED_LIB"]
  configs = [ "mojo/build/config:mojo_sdk" ]

  sources = [
    "src/mojo_dart_core.cc",
  ]
  deps = [
    "../c/environment",
    "../c/system:for_shared_library",
    "../cpp/environment:standalone",
    "../cpp/system",
    "../cpp/utility",
    "../cpp/bindings:callback",
  ]
}


copy("copy_core_library") {
  sources = [
    "$root_out_dir/libmojo_dart_core.so",
  ]
  outputs = [
    "$root_out_dir/gen/mojo/public/dart/src/libmojo_dart_core.so"
  ]
  deps =[
    ":mojo_dart_core"
  ]
}


copy("bindings") {
  sources = [
    "bindings.dart",
    "core.dart",
    "mojo_init.dart",
    "src/buffer.dart",
    "src/codec.dart",
    "src/data_pipe.dart",
    "src/handle.dart",
    "src/handle_watcher.dart",
    "src/message_pipe.dart",
    "src/types.dart",
  ]
  outputs = [
    "{{source_gen_dir}}/{{source_file_part}}"
  ]
}
